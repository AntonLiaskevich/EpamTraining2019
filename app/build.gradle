apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.example.myapplication"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        missingDimensionStrategy('environment', 'prod')
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "SERVER_PREFIX", "\"\""
        }

        debug {
            buildConfigField "String", "SERVER_PREFIX", "\"test.\""
        }
    }

    flavorDimensions "version", "api"

    productFlavors {
        demo {
            dimension "version"
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
        }

        full {
            dimension "version"
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }

        // Configurations in the "api" product flavors override those in "mode"
        // flavors and the defaultConfig block. Gradle determines the priority
        // between flavor dimensions based on the order in which they appear next
        // to the flavorDimensions property above--the first dimension has a higher
        // priority than the second, and so on.
        minApi24 {
            dimension "api"
            minSdkVersion 24
            // To ensure the target device receives the version of the app with
            // the highest compatible API level, assign version codes in increasing
            // value with API level. To learn more about assigning version codes to
            // support app updates and uploading to Google Play, read Multiple APK Support
            versionCode 30000 + android.defaultConfig.versionCode
            versionNameSuffix "-minApi24"
        }

        minApi23 {
            dimension "api"
            minSdkVersion 23
            versionCode 20000 + android.defaultConfig.versionCode
            versionNameSuffix "-minApi23"
        }

        minApi21 {
            dimension "api"
            minSdkVersion 21
            versionCode 10000 + android.defaultConfig.versionCode
            versionNameSuffix "-minApi21"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        // To check for a certain build type, use variant.buildType.name == "<buildType>"
        if (names.contains("minApi21") && names.contains("demo")) {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }

    sourceSets {
        demo {
            java {
                srcDirs = ['src/demo/java']
            }
        }

        full {
            java {
                srcDirs = ['src/full/java']
            }
        }
    }
}

task helloWorld() {
    println "Hello World"
}

task demoDepends(dependsOn: helloWorld) {
    println "demoDepends"
}

task printV() {
    doLast {
        android.applicationVariants.all { variant ->
            println variant.name
        }
    }
}

task mergeRequestFastCheck {
    dependsOn 'clean'
    dependsOn ':repositorymodule:testDebugUnitTest'
    dependsOn 'testFullMinApi21DebugUnitTest'

    doLast {
        println 'mailStart'

        if (tasks.testFullMinApi21DebugUnitTest.didWork) {
            println 'SEND E-MAIL ANNOUNCING SUCCESS'
        } else {
            println 'SEND E-MAIL ANNOUNCING FAIL'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':awesomemodule')
    implementation project(':repositorymodule')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    fullImplementation 'com.squareup.picasso:picasso:2.71828'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
